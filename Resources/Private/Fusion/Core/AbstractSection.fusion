prototype(Neos.Form.FusionRenderer:AbstractSection) < prototype(Neos.Fusion:Tag) {
    @context.containerElement = ${this.containerElement}
    tagName = 'fieldset'
    attributes {
        class = ${containerElement.properties.elementClassAttribute}
        class.@if.hasCustomClass = ${containerElement.properties.elementClassAttribute ? true : false}
    }
    content = Neos.Fusion:Join {
        legend = Neos.Fusion:Tag {
            tagName = 'legend'
            content = ${Neos.Form.FusionRenderer.translateAndEscapeProperty(containerElement, 'label')}
            @if.hasLabel = ${containerElement.label ? true : false}
        }
        elements = Neos.Fusion:Loop {
            collection = ${containerElement.elements}
            itemName = 'element'
            itemRenderer = Neos.Fusion:Renderer {
                type = ${element.type}
            }
        }
    }
    @process.renderCallbacks = Neos.Form.FusionRenderer:RenderCallbacks {
        formRuntime = ${formRuntime}
        formElement = ${containerElement}
    }
}
