prototype(Neos.Form.FusionRenderer:FormNavigation) < prototype(Neos.Fusion:Tag) {
    tagName = 'nav'
    attributes.class = 'form-navigation'
    content = Neos.Fusion:Array {
        previousButton = Neos.Form.FusionRenderer:FormNavigationButton {
            label = ${Neos.Form.FusionRenderer.translate(formRuntime, 'forms.navigation.previousPage', 'Previous page')}
            class = ${formRuntime.renderingOptions.previousButtonClassAttribute}
            containerClass = 'previous'
            fieldValue = ${formRuntime.previousPage.index}
            attributes.formnovalidate = ${true}
            @if.previousPageExists = ${formRuntime.previousPage ? true : false}
        }
        nextButton = Neos.Form.FusionRenderer:FormNavigationButton {
            label = ${Neos.Form.FusionRenderer.translate(formRuntime, 'forms.navigation.nextPage', 'Next page')}
            class = ${formRuntime.renderingOptions.nextButtonClassAttribute}
            containerClass = 'next'
            fieldValue = ${formRuntime.nextPage.index}
            @if.nextPageExists = ${formRuntime.nextPage ? true : false}
        }
        submitButton = Neos.Form.FusionRenderer:FormNavigationButton {
            label = ${formRuntime.renderingOptions.submitButtonLabel ? formRuntime.renderingOptions.submitButtonLabel : Neos.Form.FusionRenderer.translate(formRuntime, 'forms.navigation.submit', 'Submit')}
            class = ${formRuntime.renderingOptions.submitButtonClassAttribute}
            containerClass = 'next submit'
            fieldValue = ${Array.length(formRuntime.pages)}
            @if.nextPageDoesNotExist = ${!formRuntime.nextPage ? true : false}
        }
        @process.wrap = Neos.Fusion:Tag {
            tagName = 'ul'
            content = ${value}
        }
    }

    @process.wrap = Neos.Fusion:Tag {
        tagName = 'div'
        attributes.class = 'actions'
        content = ${value}
    }

}

prototype(Neos.Form.FusionRenderer:FormNavigationButton) < prototype(Neos.Fusion:Tag) {
    tagName = 'button'
    content = ${this.label}
    @context.fieldName = ${this.fieldName}
    @context.fieldValue = ${this.fieldValue}
    @context.class = ${this.class}
    @context.containerClass = ${this.containerClass}
    attributes {
        class = ${class}
        type = 'submit'
        name = ${'--' + formRuntime.identifier + '[__currentPage]'}
        value = ${fieldValue}
    }

    @process.wrap = Neos.Fusion:Tag {
        tagName = 'li'
        attributes.class = ${containerClass}
        content = ${value}
    }
}
