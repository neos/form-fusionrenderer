prototype(Neos.Form:Form) < prototype(Neos.Fusion:Tag) {
    tagName = 'form'

    attributes = Neos.Fusion:Attributes {
        method = 'post'
        enctype = 'multipart/form-data'
        id = ${formRuntime.identifier}
        action = ${request.httpRequest.uri + '#' + formRuntime.identifier}
    }

    content = Neos.Fusion:Array {
        formHiddenFields = Neos.Fusion:Array {
            formStateHiddenField = Neos.Form.FusionRenderer:FormStateHiddenField
            @process.wrap = ${'<div style="display:none">' + value + '</div>'}
        }
        pages = Neos.Fusion:Collection {
            collection = ${[formRuntime.currentPage]}
            itemName = 'page'
            itemRenderer = Neos.Fusion:Renderer {
                type = ${page.type}
                element.containerElement = ${page}
            }
        }
        navigation = Neos.Form.FusionRenderer:FormNavigation
    }
}

prototype(Neos.Form:FormEditMode) < prototype(Neos.Form:Form) {
    content.navigation >
    content.pages.collection = ${formRuntime.pages}
}

prototype(Neos.Form.FusionRenderer:FormStateHiddenField) < prototype(Neos.Fusion:Tag) {
    tagName = 'input'
    attributes {
        type = 'hidden'
        name = ${'--' + formRuntime.identifier + '[__state]'}
        value = ${formRuntime.serializedFormState}
    }
}


prototype(Neos.Form.FusionRenderer:RenderCallbacks) {
    @class = 'Neos\\Form\\FusionRenderer\\Fusion\\RenderCallbacksImplementation'
    value = ${value}
}

neos_form = Neos.Fusion:Renderer {
    type = ${formRuntime.type}
}