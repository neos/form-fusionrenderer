prototype(Neos.Form:PasswordWithConfirmation) < prototype(Neos.Form.FusionRenderer:FormElement) {
    fieldContainer {
        field {
            tagName = 'input'
            attributes {
                type = 'password'
                name = ${elementName + '[password]'}
                value = ${elementValue}
            }
        }
        confirmationLabel = Neos.Form.FusionRenderer:FormElementLabel {
            @position = 'after field'
            attributes.for = ${element.uniqueIdentifier + '-confirmation'}
            content.value = ${Neos.Form.FusionRenderer.translateAndEscapeProperty(element, 'confirmationLabel')}
        }
        confirmationField = Neos.Fusion:Tag {
            @position = 'after confirmationLabel'
            tagName = 'input'
            attributes {
                placeholder = ${element.properties.confirmationPlaceholder}
                class = Neos.Fusion:Join {
                    defaultClass = ${element.properties.confirmationClassAttribute ? element.properties.confirmationClassAttribute : null}
                    errorClass = ${element.properties.elementErrorClassAttribute ? element.properties.elementErrorClassAttribute : 'error'}
                    errorClass.@process.whitespace = ${' ' + value}
                    errorClass.@if.hasValidationError = ${elementHasValidationErrors}
                }
                id = ${element.uniqueIdentifier + '-confirmation'}
                type = 'password'
                name = ${elementName + '[confirmation]'}
                # TODO: value = ...
            }

        }
    }
}
