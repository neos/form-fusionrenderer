prototype(Neos.Form:ImageUpload) < prototype(Neos.Form.FusionRenderer:FormElement) {
    fieldContainer {
        uploadedImagePreview = Neos.Fusion:Tag {
            @position = 'before field'
            tagName = 'a'
            attributes {
                href = Neos.Fusion:ResourceUri {
                    resource = ${elementValue.resource}
                }
                target = '_blank'
            }
            content = Neos.Neos:ImageTag {
                asset = ${elementValue}
                maximumWidth = 200
            }
            @if.hasUploadedImage = ${elementValue ? true : false}
            @if.hasNoValidationErrors = ${!elementHasValidationErrors}
        }
        uploadedImageHiddenField =  Neos.Fusion:Tag {
            @position = 'before field'
            tagName = 'input'
            attributes {
                type = 'hidden'
                name = ${elementName + '[resource][originallySubmittedResource][__identity]'}
                value = ${Neos.Form.FusionRenderer.identifier(elementValue.resource)}
            }
            @if.hasUploadedResource = ${elementValue ? true : false}
        }
        field {
            tagName = 'input'
            attributes {
                type = 'file'
                accept = ${Neos.Form.FusionRenderer.getAcceptFromAllowedExtensions(element.properties.allowedTypes)}
                name = ${elementName + '[resource]'}
            }
        }
    }
}
